buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.0'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    lintOptions {
        abortOnError false
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "atsl.aar"
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:24.2.1'
    implementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    implementation 'com.android.support.test.espresso:espresso-contrib:2.2.1'
    implementation 'com.android.support:recyclerview-v7:'
    implementation 'com.android.support:support-annotations:24.2.1'
    implementation 'com.android.support.test:rules:0.4'
    implementation 'com.android.support.test:runner:0.4'
    implementation 'com.android.support:support-v4:24.2.1'
}

afterEvaluate {
    generateReleaseBuildConfig.enabled = false
}

def nexusUrl = System.getProperty('NEXUS_PROTOCOL', 'http') + '://' +
        System.getProperty('NEXUS_HOST', '') +
        System.getProperty('NEXUS_PATH', '/nexus/content/repositories/ebu-releases/')

publishing {
    repositories {
        maven {
            url nexusUrl
            credentials {
                username System.getProperty('NEXUS_USER', '')
                password System.getProperty('NEXUS_PASSWORD', '')
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId     System.getProperty('GROUP_ID', 'com.blackberry.blackberrydynamics')
            artifactId  System.getProperty('ARTIFACT_ID', 'atsl')
            version     System.getProperty('VERSION_ID', 'unknown')
            artifact ("$buildDir/outputs/aar/atsl.aar") {
                builtBy assembleRelease
            }

        }
    }
}
